if ("${CMAKE_C_COMPILER_ID}" STREQUAL "GNU" OR
    "${CMAKE_C_COMPILER_ID}" STREQUAL "Clang")
  set(warning_compile_options -pedantic -Wall -Wextra -Werror)
  if ("${CMAKE_C_COMPILER_ID}" STREQUAL "Clang")
    set(warning_compile_options ${warning_compile_options} -Weverything)
  endif()

  if (TLOCHAT_USE_ASAN)
    set(sanitizer_compile_options -fsanitize=address -fsanitize=undefined -O1 -fno-omit-frame-pointer -fno-optimize-sibling-calls)
    set(sanitizer_link_options -fsanitize=address -fsanitize=undefined)
  elseif(TLOCHAT_USE_MSAN)
    set(sanitizer_compile_options -fsanitize=memory -fsanitize=undefined -O1 -fno-omit-frame-pointer -fno-optimize-sibling-calls)
    set(sanitizer_link_options -fsanitize=memory -fsanitize=undefined)
  endif()

  if ("${CMAKE_C_COMPILER_ID}" STREQUAL "Clang")
    set(clang_compile_options -Wno-padded -Wno-disabled-macro-expansion)
  endif()
endif()

set(tlochat_server_headers client_handlers.h)
set(tlochat_server_sources client_handlers.c tlochat_server.c)
add_executable(tlochat_server ${tlochat_server_headers} ${tlochat_server_sources})
set_target_properties(tlochat_server PROPERTIES C_EXTENSIONS OFF)
target_compile_options(tlochat_server PRIVATE ${warning_compile_options} ${clang_compile_options} ${sanitizer_compile_options})
target_include_directories(tlochat_server PRIVATE ${CMAKE_CURRENT_SOURCE_DIR})
target_link_libraries(tlochat_server PRIVATE tloc tlocposix ${sanitizer_link_options})
